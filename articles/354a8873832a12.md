---
title: "Nimのウェブフレームワークをマルチスレッド化するための断片的な知識"
emoji: "👑"
type: "idea" # tech: 技術記事 / idea: アイデア
topics: ["nim"]
published: true
---

ほんとうはスクラップに書くような内容ですが、スクラップは前後の移動ができないのでこちらに書きます。
随時更新し続けます。

---

# 起動コマンド
```sh
nim c -r --threads:on --mm:orc -d:useMalloc -d:useRealtimeGC main
```

# asynchttpserverのマルチスレッド化
[asynchttpserver](https://nim-lang.org/docs/asynchttpserver.html)

これを
```nim
import
  std/asyncdispatch,
  std/asynchttpserver


proc asyncProc {.async.} =
  var server = newAsyncHttpServer()
  proc cb(req: Request) {.async.} =
    let headers = {"Content-type": "text/plain; charset=utf-8"}
    await req.respond(Http200, "Hello World", headers.newHttpHeaders())

  server.listen(Port(5000))
  while true:
    if server.shouldAcceptRequest():
      await server.acceptRequest(cb)
    else:
      await sleepAsync(500)

waitFor asyncProc()
```

こうじゃ
```nim
import
  std/asyncdispatch,
  std/asynchttpserver,
  std/osproc,  
  std/strformat


proc threadProc() {.thread.} =
  proc asyncProc() {.async.} =
    var server = newAsyncHttpServer(true, true)
    proc cb(req: Request) {.async, gcsafe.} =
      let headers = {"Content-type": "text/plain; charset=utf-8"}
      await req.respond(Http200, "Hello World", headers.newHttpHeaders())

    server.listen(Port(5000))
    while true:
      if server.shouldAcceptRequest():
        await server.acceptRequest(cb)
      else:
        await sleepAsync(500)

  while true:
    try:
      asyncCheck asyncProc()
      runForever()
    except:
      echo repr(getCurrentException())

proc serve() =
  when compileOption("threads"):
    let countThreads = countProcessors()
    echo(&"Starting {countThreads} threads")
    var thr = newSeq[Thread[void]](countThreads)
    for i in 1..countThreads:
      createThread(thr[i-1], threadProc)
    joinThreads(thr)
  else:
    threadProc()

serve()
```

# 実行時にセットされる別ファイルに定義してある環境変数をスレッド内から呼ぶ

```nim:environment.nim
import os

let AppEnv* = getEnv("APP_ENV")
```

```nim:main.nim
import
  os,
  osproc,
  strformat,
  ./environment

proc thread(nThread:int) {.thread.} =
  for i in 1..5:
    echo &"スレッド{nThread} {i}回目"
    echo AppEnv
    sleep(1000)

proc main() =
  let countThreads = countProcessors()
  var thr = newSeq[Thread[int]](countThreads)
  for i in 1..countThreads:
    createThread(thr[i-1], thread, i)
  joinThreads(thr)

main()
```

コンパイルエラーになる
```sh
APP_ENV="aaa" nim c -r --threads:on --mm:orc -d:useMalloc -d:useRealtimeGC main

> main.nim(6, 6) Error: 'thread' is not GC-safe as it accesses 'AppEnv' which is a global using GC'ed memory
```

---

環境変数にアクセスしているところを `{.gcsafe.}` のプラグマを付けてあげる

```diff nim:main.nim
import
  os,
  osproc,
  strformat,
  ./environment

proc thread(nThread:int) {.thread.} =
  for i in 1..3:
    echo &"スレッド…{nThread} {i}回目"
+   {.gcsafe.}:
+     echo AppEnv
    sleep(1000)

proc main() =
  let countThreads = countProcessors()
  var thr = newSeq[Thread[int]](countThreads)
  for i in 1..countThreads:
    createThread(thr[i-1], thread, i)
  joinThreads(thr)

main()
```

```sh: 出力
スレッド…2 1回目
aaa
スレッド…4 1回目
aaa
スレッド…1 1回目
aaa
スレッド…3 1回目
aaa
スレッド…2 2回目
aaa
スレッド…3 2回目
aaa
スレッド…1 2回目
aaa
スレッド…4 2回目
aaa
スレッド…2 3回目
aaa
スレッド…3 3回目
aaa
スレッド…1 3回目
aaa
スレッド…4 3回目
aaa
```

# スレッド内からRDBにアクセスする
クエリビルダには私が作った[alllographer](https://github.com/itsumura-h/nim-allographer)を使います

```nim: environment.nim
import
  std/asyncdispatch,
  std/random,
  std/json,
  allographer/connection,
  allographer/schema_builder,
  allographer/query_builder

let rdb* = dbOpen(PostgreSQL, "db_name", "user", "pass", "db_host", 5432, 20, 30, true, false)

randomize()

# マイグレーション
rdb.create(
  table("hello", [
    Column.integer("id"),
    Column.integer("randomnumber")
  ])
)

# シーダー
seeder rdb, "hello":
  var data = newSeq[JsonNode]()
  for i in 1..10000:
    let randomNum = rand(10000)
    data.add(%*{"id": i, "randomnumber": randomNum})
  rdb.table("hello").insert(data).waitFor
```

```nim: main.nim
import
  std/asyncdispatch,
  std/json,
  std/osproc,
  allographer/query_builder,
  ./environment

proc thread(nThread:int) {.thread.} =
  (proc() {.async.} =
    for i in 1..3:
      let res = rdb.table("hello").find(i).await.get
      echo &"スレッド{nThread} {i}回目 {res}"
  )().waitFor


proc main() =
  when compileOption("threads"):
    let countThreads = countProcessors()
    var thr = newSeq[Thread[int]](countThreads)
    for i in 1..countThreads:
      createThread(thr[i-1], thread, i)
    joinThreads(thr)
  else:
    thread(1)

main()
```

コンパイルエラーになる
```sh
DB_POSTGRES=true nim c -r --threads:on --mm:orc -d:useMalloc -d:useRealtimeGC main

> main.nim(9, 13) Error: 'thread' is not GC-safe as it performs an indirect call here
```


## `{.gcsafe.}`プラグマを付ける

```diff nim:main.nim
import
  std/asyncdispatch,
  std/json,
  std/locks,
  std/os,
  std/osproc,
  allographer/query_builder,
  ./environment

proc thread(nThread:int:int) {.thread.} =
  (proc() {.async.} =
    for i in 1..3:
+     {.gcsafe.}:
        let res = rdb.table("hello").find(i).await.get
        echo &"スレッド{nThread} {i}回目 {res}"
  )().waitFor


proc main() =
  when compileOption("threads"):
    let countThreads = countProcessors()
    var thr = newSeq[Thread[int]](countThreads)
    for i in 1..countThreads:
      createThread(thr[i-1], thread, i)
    joinThreads(thr)
  else:
    thread(1)

main()
```

セグフォが起きたり
```sh
SIGSEGV: Illegal storage access. (Attempt to read from nil?)
Segmentation fault (core dumped)
```

複数のクエリの実行が混ざったりしてエラーが起きる
```sh
ERROR:  syntax error at or near "FROM"
LINE 1: SELECT * FROM "hello" LIMIT 1 FROM "hello" LIMIT 1
```

## Lockを使う
[locks](https://nim-lang.org/docs/locks.html)  
[threadsによるlockのサンプル](https://nim-lang.org/docs/threads.html)

```nim: lockのサンプル
import std/locks

var
  thr: array[0..4, Thread[tuple[a,b: int]]]
  L: Lock

proc threadFunc(interval: tuple[a,b: int]) {.thread.} =
  for i in interval.a..interval.b:
    acquire(L) # lock stdout
    echo i
    release(L)

initLock(L)

for i in 0..high(thr):
  createThread(thr[i], threadFunc, (i*10, i*10+5))
joinThreads(thr)

deinitLock(L)
```

これにより複数のスレッドが同時にDBへのコネクションのインスタンスを扱わないようにする。

```diff nim:environment.nim
import
  std/asyncdispatch,
+ std/locks,
  std/random,
  std/json,
  allographer/connection,
  allographer/schema_builder,
  allographer/query_builder

+ var L*: Lock
+ initLock(L)
let rdb* = dbOpen(PostgreSQL, "db_name", "user", "pass", "db_host", 5432, 20, 30, true, false)

randomize()

rdb.create(
  table("hello", [
    Column.integer("id"),
    Column.integer("randomnumber")
  ])
)

seeder rdb, "hello":
  var data = newSeq[JsonNode]()
  for i in 1..10000:
    let randomNum = rand(10000)
    data.add(%*{"id": i, "randomnumber": randomNum})
  rdb.table("hello").insert(data).waitFor
```

```diff nim:main.nim
import
  std/asyncdispatch,
  std/json,
+ std/locks,
  std/options,
  std/osproc,
  std/strformat,
  allographer/query_builder,
  ./environment

proc thread(nThread:int) {.thread.} =
  (proc() {.async.} =
    for i in 1..3:
+     withLock(L):
        {.gcsafe.}:
          let res = rdb.table("hello").find(i).await.get
          echo &"スレッド{nThread} {i}回目 {res}"
  )().waitFor


proc main() =
  when compileOption("threads"):
    let countThreads = countProcessors()
    var thr = newSeq[Thread[int]](countThreads)
    for i in 1..countThreads:
      createThread(thr[i-1], thread, i)
    joinThreads(thr)
  else:
    thread(1)

main()
+ deinitLock(L)
```

```sh: 出力
スレッド1 1回目 {"id":1,"randomnumber":2}
スレッド1 2回目 {"id":2,"randomnumber":36}
スレッド1 3回目 {"id":3,"randomnumber":4355}
スレッド3 1回目 {"id":1,"randomnumber":2}
スレッド4 1回目 {"id":1,"randomnumber":2}
スレッド4 2回目 {"id":2,"randomnumber":36}
スレッド4 3回目 {"id":3,"randomnumber":4355}
スレッド2 1回目 {"id":1,"randomnumber":2}
スレッド2 2回目 {"id":2,"randomnumber":36}
スレッド2 3回目 {"id":3,"randomnumber":4355}
スレッド3 2回目 {"id":2,"randomnumber":36}
スレッド3 3回目 {"id":3,"randomnumber":4355}
```

正常に全てのクエリが実行されるが、スレッドずつ順番に実行するため、速度が遅い。マルチスレッド化している意味がない

## Lockは使わず、子スレッド内でRDBへのコネクションを作る
```diff nim:environment.nim
import
  std/asyncdispatch,
  std/random,
  std/json,
  allographer/connection,
  allographer/schema_builder,
  allographer/query_builder

+ proc initDb*():Rdb =
+   return dbOpen(PostgreSQL, "db_name", "user", "pass", "db_host", 5432, 20, 30, true, false)

randomize()

+ let rdb = initDb()
rdb.create(
  table("hello", [
      Column.integer("id"),
      Column.integer("randomnumber")
  ])
)

seeder rdb, "hello":
  var data = newSeq[JsonNode]()
  for i in 1..10000:
    let randomNum = rand(10000)
    data.add(%*{"id": i, "randomnumber": randomNum})
  rdb.table("hello").insert(data).waitFor
```

```diff nim:main.nim
import
  std/asyncdispatch,
  std/json,
  std/options,
  std/osproc,
  std/strformat,
  allographer/query_builder,
  ./environment

proc thread(nThread:int) {.thread.} =
+ let rdb = initDb()
  (proc() {.async.} =
    for i in 1..3:
      {.gcsafe.}:
        let res = rdb.table("hello").find(i).await.get
        echo &"スレッド{nThread} {i}回目 {res}"
  )().waitFor


proc main() =
  when compileOption("threads"):
    let countThreads = countProcessors()
    var thr = newSeq[Thread[int]](countThreads)
    for i in 1..countThreads:
      createThread(thr[i-1], thread, i)
    joinThreads(thr)
  else:
    thread(1)

main()
```

```sh: 出力
スレッド3 1回目 {"id":1,"randomnumber":2}
スレッド3 2回目 {"id":2,"randomnumber":36}
スレッド2 1回目 {"id":1,"randomnumber":2}
スレッド3 3回目 {"id":3,"randomnumber":4355}
スレッド4 1回目 {"id":1,"randomnumber":2}
スレッド1 1回目 {"id":1,"randomnumber":2}
スレッド2 2回目 {"id":2,"randomnumber":36}
スレッド4 2回目 {"id":2,"randomnumber":36}
スレッド1 2回目 {"id":2,"randomnumber":36}
スレッド2 3回目 {"id":3,"randomnumber":4355}
スレッド4 3回目 {"id":3,"randomnumber":4355}
スレッド1 3回目 {"id":3,"randomnumber":4355}
```
スレッド同士が競合せずにDBアクセスできるようになったが、フルスタックWebフレームワークのデザイン上、どう子スレッドの中でコネクションを作り開発者が触るところへ出すかは難しい。


## スレッド毎にPluginという概念を持ち、そこにコネクションを持たせる

```diff nim:environment.nim
import
  std/asyncdispatch,
  std/random,
  std/json,
  allographer/connection,
  allographer/schema_builder,
  allographer/query_builder

proc initDb*():Rdb =
  return dbOpen(PostgreSQL, "db_name", "user", "pass", "db_host", 5432, 20, 30, true, false)

+ type Plugin* = ref object
+   rdb*: Rdb

randomize()

var rdb = initDb()
rdb.create(
  table("World", [
    Column.integer("id"),
    Column.integer("randomnumber")
  ])
)

seeder rdb, "World":
  var data = newSeq[JsonNode]()
  for i in 1..10000:
    let randomNum = rand(10000)
    data.add(%*{"id": i, "randomnumber": randomNum})
  rdb.table("World").insert(data).waitFor
```

```diff nim:controller.nim
import
  std/asyncdispatch,
  std/json,
  std/options,
  std/strformat,
  allographer/query_builder,
  ./environment

+ proc controller*(param: (Plugin, int)):Future[void] {.async, gcsafe.} =
+   let (plugin, nThread) = param
    for i in 1..3:
      let res = plugin.rdb.table("hello").find(i).await.get
      echo &"スレッド{nThread} {i}回目 {res}"
```

```diff nim:main.nim
import
  std/asyncdispatch,
  std/json,
  std/osproc,
  ./environment,
  ./controller

+ proc thread(param:(Plugin, int)) {.thread.} =
    (proc() {.async.} =
      await controller.controller(param)
    )().waitFor


+ proc main(plugins:seq[Plugin]) =
    when compileOption("threads"):
      let countThreads = countProcessors()
+     var thr = newSeq[Thread[(Plugin, int)]](countThreads)
+     for i in 0..countThreads-1:
+       createThread(thr[i], thread, (plugins[i], i+1))
      joinThreads(thr)
    else:
+     thread((plugins[0], 1))

+ var plugins = newSeq[Plugin](countProcessors())
+ for i in 0..countProcessors()-1:
+   plugins[i] = Plugin(
+     rdb:initDb()
+   ) 
+ main(plugins)
```

```sh: 出力
スレッド2 1回目 {"id":1,"randomnumber":2}
スレッド3 1回目 {"id":1,"randomnumber":2}
スレッド1 1回目 {"id":1,"randomnumber":2}
スレッド4 1回目 {"id":1,"randomnumber":2}
スレッド2 2回目 {"id":2,"randomnumber":36}
スレッド3 2回目 {"id":2,"randomnumber":36}
スレッド1 2回目 {"id":2,"randomnumber":36}
スレッド4 2回目 {"id":2,"randomnumber":36}
スレッド3 3回目 {"id":3,"randomnumber":4355}
スレッド1 3回目 {"id":3,"randomnumber":4355}
スレッド4 3回目 {"id":3,"randomnumber":4355}
スレッド2 3回目 {"id":3,"randomnumber":4355}
```

スレッドの数だけPluginの配列を作り、各スレッドにはPluginを渡す。
こうすることでスレッド同士が同じPluginを触るのを防ぐ。
メモリ管理はスレッド単位で行われる。
Pluginを通してコントローラーの中でrdbへのインスタンスを使う。
